apply plugin: 'com.android.application'

version = "0.2.9"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.makina.ecrins.flora"
        minSdkVersion 10
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionName version
        buildConfigField "String", "BUILD_DATE", "\"" + new Date().getTime() + "\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEV"
        }

        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true

            applicationVariants.all { variant ->
                variant.outputs.each  { output ->
                    // noinspection GroovyAssignabilityCheck
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':commons')
    compile project(':maps')

    compile "com.android.support:appcompat-v7:${rootProject.ext.androidSupportVersion}"
}

task('increaseVersionCode') << {
    getVersionCode(module.name + "/src/main/AndroidManifest.xml", true)
}

tasks.whenTaskAdded { task ->
    if (task.name == 'generateReleaseBuildConfig' || task.name == 'generateDebugBuildConfig') {
        task.dependsOn 'increaseVersionCode'
    }
}