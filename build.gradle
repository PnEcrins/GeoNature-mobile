// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        // this order matters
        mavenCentral()
        jcenter()
        google()
    }
}

ext {
    minSdkVersion = 15
    compileSdkVersion = 27
    buildToolsVersion = '27.0.2'
    androidSupportVersion = '27.1.0'
}

File getVersionPropertiesFile(moduleName) {
    def versionPropsFile = file(moduleName + '/version.properties')

    if (!versionPropsFile.exists()) {
        versionPropsFile.createNewFile()
    }

    return versionPropsFile
}

Properties readVersionProperties(moduleName) {
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(getVersionPropertiesFile(moduleName)))

    if (!versionProps['VERSION_CODE']) {
        versionProps['VERSION_CODE'] = '1'
    }

    return versionProps
}

int getVersionCode(moduleName) {
    Properties versionProps = readVersionProperties(moduleName)

    return Integer.parseInt(versionProps['VERSION_CODE'].toString()).intValue()
}

int updateVersionCode(moduleName) {
    Properties versionProps = readVersionProperties(moduleName)
    def versionCode = getVersionCode(moduleName) + 1

    versionProps['VERSION_CODE'] = versionCode.toString()
    versionProps.store(getVersionPropertiesFile(moduleName).newWriter(), null)

    println "updateVersionCode: ${versionCode}"

    return versionCode
}
